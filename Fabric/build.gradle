plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = file("src/main/resources/travelstaff-fabric.accessWidener")
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    common(project(path: ":Common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":Common", configuration: "transformProductionFabric")) { transitive false }

    modImplementation group: 'ca.lukegrahamlandry.lib', name: 'WrapperLib-1.19-Fabric', version: '1.0.0'
    shadowCommon group: 'ca.lukegrahamlandry.lib', name: 'WrapperLib-1.19-Fabric', version: '1.0.0'
}

processResources {
    filesMatching("fabric.mods.json") {
        expand "version": project.mod_version
    }
    inputs.property "fabric.mods.json", project.mod_version
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"  // not obfuscated, with shadowed stuff

    relocate 'ca.lukegrahamlandry.lib', rootProject.group + '.shadow.wrapperlib'
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null  // obfuscated, with shadowed stuff
}

task renameJarForPublication(type: Zip, dependsOn: remapJar) {
    from remapJar.archiveFile.map { zipTree(it) }
    extension "jar"
    metadataCharset "UTF-8"
    destinationDirectory = base.libsDirectory
    classifier project.name
}

assemble.dependsOn renameJarForPublication

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

apply from: "https://moddingtutorials.org/publish.gradle"