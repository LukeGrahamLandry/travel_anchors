plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

loom {
    accessWidenerPath = project(":Common").loom.accessWidenerPath
    forge {
        // mixinConfig "modid.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"

    common(project(path: ":Common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":Common", configuration: "transformProductionForge")) { transitive false }

    modImplementation(group: 'ca.lukegrahamlandry.lib', name: 'WrapperLib-1.19.3-Forge', version: '1.0.0') { transitive false }
    shadowCommon(group: 'ca.lukegrahamlandry.lib', name: 'WrapperLib-1.19.3-Forge', version: '1.0.0') { transitive false }
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand "version": project.mod_version
    }
    inputs.property "META-INF/mods.toml", project.mod_version
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"  // not obfuscated, with shadowed stuff

    exclude 'ca/lukegrahamlandry/lib/event/forge/WrapperLibModMain.class'
    exclude 'ca/lukegrahamlandry/lib/helper/'
    exclude 'ca/lukegrahamlandry/lib/data/'
    relocate 'ca.lukegrahamlandry.lib', rootProject.group + '.shadow.wrapperlib'

}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null  // obfuscated, with shadowed stuff
}

task renameJarForPublication(type: Zip, dependsOn: remapJar) {
    from remapJar.archiveFile.map { zipTree(it) }
    extension "jar"
    metadataCharset "UTF-8"
    destinationDirectory = base.libsDirectory
    classifier project.name
}

assemble.dependsOn renameJarForPublication

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

apply from: "https://moddingtutorials.org/publish.gradle"